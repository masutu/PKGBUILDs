# Maintainer: masutu <masutu dot arch at gmail dot com>
# Contributor: masutu <masutu dot arch at gmail dot com>
pkgname=mmass
pkgver=5.2.0
pkgrel=1
pkgdesc="Open source tool for precise mass spectrometric data analysis and interpretation written in python."
arch=('i686' 'x86_64')
url="http://www.mmass.org"
license=('GPL')
depends=('python2-numpy' 'wxpython')
source=(${url}/download/files/v${pkgver}/${pkgname}_v${pkgver}_source.zip)
md5sums=('828c22cf850145eb9b86d3ba13fa4ebe')

build() {
  cd "$srcdir/mMass/mspy"
  python2 setup.py build
  cp build/lib.linux-*-*/calculations.so .
}

package() {
  cd "$srcdir/mMass"
  install -m 0755 -d $pkgdir/usr/bin
  install -m 0755 -d $pkgdir/usr/share/$pkgname
  install -m 0755 -d $pkgdir/usr/share/$pkgname/{configs,gui,mspy}
  install -m 0755 -d $pkgdir/usr/share/$pkgname/gui/images/gtk
  install -m 0644 mmass.py $pkgdir/usr/share/$pkgname
  install -m 0644 configs/*.xml $pkgdir/usr/share/$pkgname/configs
  install -m 0644 gui/*.py $pkgdir/usr/share/$pkgname/gui
  install -m 0644 gui/images/gtk/*.png $pkgdir/usr/share/$pkgname/gui/images/gtk
  install -m 0644 mspy/*.py $pkgdir/usr/share/$pkgname/mspy
  install -m 0755 mspy/calculations.so $pkgdir/usr/share/$pkgname/mspy
  cat >$pkgdir/usr/bin/mmass <<EOF
#!/bin/sh
python2 /usr/share/mmass/mmass.py ""
EOF
  chmod 0755 $pkgdir/usr/bin/mmass
}

# vim:set ts=2 sw=2 et:
